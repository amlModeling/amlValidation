import "AMLValidatorUtils.evl";


context CAEX!ExternalInterface
{
	constraint HasOrderInstanceDirection
	{	
		guard: self.isDerivedFromOrder()
		
		check
		{
			debugOutput("HasOrderInstanceDirection: " + self);
		
			var bRet : Boolean; 
			var bIsChildOfOrder : Boolean;
			var ergMap : Map;
			var caexFile : CAEXFile;
			var attr : Attribute;			 	
			
			caexFile = self.getCAEXFile();
			
			return self.hasDirection();
			
		}
				
		message: ValidationExecution.addError(caexFile.filename, "", "", "HasOrderInstanceDirection", "Instance of Type Order doesn't have attribute Direction: " + self.name)
	}
	
	
	constraint HasOrderInstanceDirectionCorrectValue
	{
		guard: self.satisfies("HasOrderInstanceDirection")
		
		check
		{
			debugOutput("HasOrderInstanceDirectionCorrectValue: " + self);
		
			var bRet : Boolean;		
			var ergMap : Map;
			var caexFile : CAEXFile;
			var attr : Attribute;		 	
			
			caexFile = self.getCAEXFile();
			
			return self.checkValueDirection();				
		
		}	
							
		message: ValidationExecution.addError(caexFile.filename, "", "", "HasOrderInstanceDirectionCorrectValue", "Instance of Type Order has invalid value Instance: " + self.name)
	
	}
	
	constraint hasExternalDataConnectorRefURI
	{
		guard: self.isDerivedFromExternalDataConnector()
		
		check
		{
			debugOutput("hasExternalDataConnectorRefURI: " + self);
		
			var bRet : Boolean; 
			var bIsChildOfOrder : Boolean;
			var ergMap : Map;
			var caexFile : CAEXFile;
			var attr : Attribute;			 	
			
			caexFile = self.getCAEXFile();
			
			if(searchAttribute(self.attribute, "refURI", false, ergMap))
				return true;
			else
				return false;
			
		}
				
		message: ValidationExecution.addError(caexFile.filename, "", "", "HasExternalDataConnectorRefURI", "Instance of Type ExternalDataConnector doesn't have attribute refURI: " + self.name)
	}
	
	constraint hasExternalDataConnectorRefUIIValue
	{
		guard: self.satisfies("hasExternalDataConnectorRefURI")
		
		check
		{
			debugOutput("hasExternalDataConnectorRefUIIValue: " + self);
		
			var bRet : Boolean;		
			var ergMap : Map;
			var caexFile : CAEXFile;
			var attr : Attribute;		 	
			
			caexFile = self.getCAEXFile();
			
		
			
			searchAttribute(self.attribute, "refURI", false, ergMap);			
			attr = ergMap.get("Attr");
			
			if (attr.value = null)
				return false;
			else
				return true;				
		
		}	
							
		message: ValidationExecution.addError(caexFile.filename, "", "", "HasExternalDataConnectorRefURI", "Instance of Type ExternalDataConnector has empty refURI: " + self.name)
	}
}


context CAEX!SystemUnitClass
{
	constraint hasPortDirectionCorrectValue
	{
		guard: self.isPortInstance() and self.hasDirection()
		
		check
		{
			debugOutput("hasPortDirectionCorrectValue: " + self);
		
			var bRet : Boolean;
			var mapPath : Map;				
			var ergMap : Map;
			var caexFile : CAEXFile;
			var attr : Attribute;		 	
			
			caexFile = self.getCAEXFile();
			
			return self.checkValueDirection();				
		
		}	
							
		message: ValidationExecution.addError(caexFile.filename, "", "", "HasPortDirectionCorrectValue", "Instance of Type Port has invalid value Instance: " + self.name)
	}
	
	constraint hasPortCardinalityCorrectValue
	{
		guard: self.isPortInstance() and  self.hasCardinality()
		
		check
		{
			debugOutput("hasPortDirectionCorrectValue: " + self);
		
			var bRet : Boolean;
			var mapPath : Map;				
			var ergMap : Map;
			var caexFile : CAEXFile;
			var attr : Attribute;		 	
			
			caexFile = self.getCAEXFile();
			
			return self.checkValueCardinality();				
		
		}	
							
		message: ValidationExecution.addError(caexFile.filename, "", "", "HasPortCardinalityCorrectValue", "Instance of Type Port has invalid value Cardinality Instance: " + self.name)
	}
	
	constraint hasPortConnector
	{
		guard: self.isPortInstance()
		
		check
		{
			debugOutput("hasPortConnector: " + self);		
			var caexFile : CAEXFile;			
			caexFile = self.getCAEXFile();
			
			for (extInt : ExternalInterface in self.externalInterface)
			{			
				if(extInt.isDerivedFromPortConnector())
				{
					return true;
				}
			}
			
			return false;
		}	
							
		message: ValidationExecution.addError(caexFile.filename, "", "", "HasPortConnector", "Instance of Type Port has no ExternalInterface PortConnector: " + self.name)
	}
	
	constraint hasPortNoChildInternalElements
	{
		guard: self.isPortInstance()
		
		check
		{
			debugOutput("hasPortNoChildInternalElements: " + self);		
			var caexFile : CAEXFile;			
			caexFile = self.getCAEXFile();
			
			if(self.internalElement.isEmpty())
				return true;
			
			return false;		
		}	
							
		message: ValidationExecution.addError(caexFile.filename, "", "", "HasPortNoChildInternalElements", "Instance of Type Port has child elements InternalElement: " + self.name)
	}
	
	constraint hasPortExternalInterface
	{
		guard: self.isPortInstance()
		
		check
		{
			debugOutput("hasPortExternalInterface: " + self);		
			var caexFile : CAEXFile;			
			caexFile = self.getCAEXFile();
			
			for (extInt : ExternalInterface in self.externalInterface)
			{			
				if(not extInt.isDerivedFromPortConnector())
					return true;					
			}
			
			return false;
			
		}	
							
		message: ValidationExecution.addError(caexFile.filename, "", "", "HasPortExternalInterface", "Port without ExternalInterface (exkl PortConnector): " + self.name)
	}
	
	
	
	
}

context CAEX!InternalLink
{
	constraint AreConnectedPortOfSameCategory
	{
		guard: self.bothPortsHaveCategories()	
	
		check
		{
			debugOutput("AreConnectedPortOfSameCategory: " + self);
			
			var caexFile : CAEXFile;
			var validationError : String;
			
			caexFile = self.getCAEXFile();
			validationError = self.bothPortsHaveSameCategories();
			return validationError.length() == 0;
		}		
		
		message 
		{
			return ValidationExecution.addError(caexFile.filename, "", "", "AreConnectedPortOfSameCategory", "Non matching Categories: " + validationError);
		}	
	}

}




context CAEX!InternalElement
{
	constraint AreFacetAttributesValid
	{
		guard: self.isFacetInstance()
		
		check
		{
			debugOutput("AreFacetAttributesValid: " + self);
		
			var bRet : Boolean;
			var mapPath : Map;				
			var ergMap : Map;
			var caexFile : CAEXFile;
			var attr : Attribute;		 	
			
			caexFile = self.getCAEXFile();
			
			return self.areFacetAttributesCorrect(ergMap);				
		
		}	
							
		message: ValidationExecution.addError(caexFile.filename, "", "", "AreFacetAttributesValid", "Facet Attributes not defined: " + ergMap.get("ErrorMsg"))
	}
	
	constraint AreFacetInterfacesValid
	{
		guard: self.isFacetInstance()
		
		check
		{
			debugOutput("AreFacetInterfacesValid: " + self);
		
			var bRet : Boolean;
			var mapPath : Map;				
			var ergMap : Map;
			var caexFile : CAEXFile;
			var attr : Attribute;		 	
			
			caexFile = self.getCAEXFile();
			
			return self.areFacetInterfacesCorrect(ergMap);				
		
		}	
							
		message: ValidationExecution.addError(caexFile.filename, "", "", "AreFacetInterfacesValid", "Facet Interfaces not defined: " + ergMap.get("ErrorMsg"))
	}
	
	constraint NoAdditionalChildElementsInFacets
	{
		guard: self.isFacetInstance()
		
		check
		{
			debugOutput("NoAdditionalChildElementsInFacets: " + self);
		
			var bRet : Boolean;
			var mapPath : Map;				
			var ergMap : Map;
			var caexFile : CAEXFile;
			var attr : Attribute;		 	
			
			caexFile = self.getCAEXFile();
			
			return self.checkInvalidChildsInFacet(ergMap);				
		
		}	
							
		message: ValidationExecution.addError(caexFile.filename, "", "", "NoAdditionalChildElementsInFacets", "Invalid childs in Facet " + self.name + ": " + ergMap.get("ErrorMsg"))
	}
	
	constraint FacetNameIsUniqueWithinSiblings
	{
		guard: self.isFacetInstance()
		
		check
		{
			debugOutput("FacetNameIsUniqueWithinSiblings: " + self);
		
			var bRet : Boolean;
			var mapPath : Map;				
			var ergMap : Map;
			var caexFile : CAEXFile;
			var attr : Attribute;		 	
			
			caexFile = self.getCAEXFile();
			
			return self.facetNameIsUniqueWithinSiblings(ergMap);				
		
		}	
							
		message: ValidationExecution.addError(caexFile.filename, "", "", "FacetNameIsUniqueWithinSiblings", ergMap.get("ErrorMsg"))
	}
}


context CAEX!InternalElement
{
	constraint AreGroupChildrenValid
	{
		guard: self.isGroupInstance()
		
		check
		{
			debugOutput("AreGroupChildrenValid: " + self);
		
			var bRet : Boolean;
			var mapPath : Map;				
			var ergMap : Map;
			var caexFile : CAEXFile;
			var attr : Attribute;		 	
			
			caexFile = self.getCAEXFile();
			
			return self.checkInvalidChildsInGroup(ergMap);	
		}	
							
		message: ValidationExecution.addError(caexFile.filename, "", "", "AreGroupChildrenValid", ergMap.get("ErrorMsg"))
	}
	
}


context CAEX!InternalElement
{
	constraint NoAdditionalChildElementsInPropertySet
	{
		guard: self.isPropertySetInstance()
		
		check
		{
			debugOutput("NoAdditionalChildElementsInPropertySet: " + self);
		
			var bRet : Boolean;
			var mapPath : Map;				
			var ergMap : Map;
			var caexFile : CAEXFile;
			var attr : Attribute;		 	
			
			caexFile = self.getCAEXFile();
			
			return self.checkInvalidChildsInProperySet(ergMap);				
		
		}	
							
		message: ValidationExecution.addError(caexFile.filename, "", "", "NoAdditionalChildElementsInFacets", "Invalid childs in PropertySet " + self.name + ": " + ergMap.get("ErrorMsg"))
	}
	
}




/* End Validations */
/************************************************************************************************************************************/





/* Start helper functions*/
/************************************************************************************************************************************/


operation CAEXObject checkValueDirection() : Boolean
{
	var ergMap : Map;
	var attr : Attribute;

	if (self.isKindOf(SystemUnitClass) or self.isTypeOf(ExternalInterface))
	{	
		searchAttribute(self.attribute, "Direction", false, ergMap);			
		attr = ergMap.get("Attr");
		
		if (attr.value == "In" or attr.value == "Out" or attr.value == "InOut")
			return true;
	
	}
	
	return false;
}

operation CAEXObject checkValueCardinality() : Boolean
{
	var ergMap : Map;
	var minOccur: Attribute;
	var maxOccur: Attribute;
	var cardinality: Attribute;
	var iMin :  Integer;
	var iMax : Integer; 

	if (self.isKindOf(SystemUnitClass))
	{	
		searchAttribute(self.attribute, "Cardinality", false, ergMap);			
		cardinality = ergMap.get("Attr");
		
		searchAttribute(cardinality.attribute, "MinOccur", false, ergMap);
		minOccur = ergMap.get("Attr");	
		
		searchAttribute(cardinality.attribute, "MaxOccur", false, ergMap);
		maxOccur = ergMap.get("Attr");
		
		
		if( minOccur == null or maxOccur == null)
			return false;
		if (minOccur.value == null or maxOccur.value == null)
			return false;	
		
		if (minOccur.value.isInteger() and maxOccur.value.isInteger())
		{
			iMin = minOccur.value.asInteger();
			iMax = maxOccur.value.asInteger();
			if (iMin >= 0 and ((iMax > iMin) or iMax == 0))
				return true;
		}
	}
	
	return false;
}

operation CAEXObject hasCardinality(): Boolean
{
	var ergMap : Map;
	
	if (self.isKindOf(SystemUnitClass))
	{	
		if(searchAttribute(self.attribute, "Cardinality", false, ergMap))			
			return true;		
	}	

	return false;
}



operation CAEXObject hasDirection(): Boolean
{
	var ergMap : Map;
	
	if (self.isKindOf(SystemUnitClass) or self.isTypeOf(ExternalInterface))
	{	
		if(searchAttribute(self.attribute, "Direction", false, ergMap))
			return true;
	}	

	return false;
}


operation SystemUnitClass isPortInstance() : Boolean
{
	var boolIEPort = self.isTypeOf(InternalElement) and self.roleRequirements.isDefined() and self.hasRoleRequirementPort(); 
	var boolSUCPort = self.isTypeOf(SystemUnitClass) and self.supportedRoleClass.isDefined() and self.hasSupportedRoleClassPort();
	var isPort = boolIEPort or boolSUCPort;
	
	return isPort;
}


operation SystemUnitClass isFacetInstance() : Boolean
{
		
	var isFacet = self.roleRequirements.isDefined() and self.hasRoleRequirementFacet();
	
	return isFacet;
}


operation InternalElement isGroupInstance() : Boolean
{		
	var isGroup = self.roleRequirements.isDefined() and self.hasRoleRequirementGroup();
	
	return isGroup;
}







operation InternalElement areFacetAttributesCorrect(ergMap : Map) : Boolean
{
	var base = self.eContainer();
	var errorText : String;
	var facetAttributesCorrect : Boolean = true;
	
	for (actAttribute : Attribute in self.attribute)
	{
		var bFound : Boolean = false;
	
		for(baseAttribute : Attribute in base.attribute)
		{
			if(actAttribute.name == baseAttribute.name)
				bFound = true;
		}
		
		if (not bFound)
		{
			errorText+= actAttribute.name + " ";
			facetAttributesCorrect = false;		
		}
	}
	
	ergMap.put("ErrorMsg", errorText);
	return facetAttributesCorrect;
}


operation InternalElement areFacetInterfacesCorrect(ergMap : Map) : Boolean
{
	var base = self.eContainer();	
	var errorText : String;
	var facetInterfaceCorrect : Boolean = true;	
	
	for(aktInterface : ExternalInterface in self.externalInterface)
	{
		var bFound  : Boolean = false;
		
		for(baseInterface : ExternalInterface in base.externalInterface)
		{
			if(aktInterface.name == baseInterface.name)
				bFound = true;
		}
		
		if (not bFound)
		{
			errorText += aktInterface.name + " ";
			facetInterfaceCorrect = false;		
		}				
	}
		
	ergMap.put("ErrorMsg", errorText);	
	return facetInterfaceCorrect;
}




operation InternalElement checkInvalidChildsInFacet(ergMap : Map) : Boolean
{
	var errorText : String;
	var isValid : Boolean = true;	
		
	if(self.internalElement.size() > 0)
	{
		errorText = "InternalElement";
		isValid = false;
	}		
	
	ergMap.put("ErrorMsg", errorText);	
	return isValid;
}

operation InternalElement checkInvalidChildsInProperySet(ergMap : Map) : Boolean
{
	var errorText : String;
	var isValid : Boolean = true;	
		
	if(self.internalElement.size() > 0)
	{
		errorText = "InternalElement ";
		isValid = false;
	}
	
	if(self.attribute.size() > 0)
	{
		errorText+= "Attribut ";
		isValid = false;
	}	
	
	if(self.externalInterface.size() > 0)
	{
		errorText+= "Interface ";
		isValid = false;
	}		
	
	errorText = errorText.trim();
	
	ergMap.put("ErrorMsg", errorText);	
	return isValid;
}


operation InternalElement facetNameIsUniqueWithinSiblings(ergMap : Map) : Boolean
{
	var base = self.eContainer();
	var isValid : Boolean = true;
	
	for (actInteralElement : InternalElement in base.internalElement)
	{
		if(actInteralElement <> self and actInteralElement.isFacetInstance())
		{
			if(self.name = actInteralElement.name)
				isValid = false;
		}
	}
	
	if(not isValid)
		ergMap.put("ErrorMsg", "Duplicate Facet name detected");
		
	return isValid;
}




operation InternalElement checkInvalidChildsInGroup(ergMap : Map) : Boolean
{
	var errorText : String;
	var isValid : Boolean = true;
	var mapPath : Map;
	
	for (actIE : InternalElement in self.internalElement)
	{
		if (actIE.refBaseSystemUnitPath.isDefined())
		{
			mapPath = parsePath(actIE.refBaseSystemUnitPath);
			isValid = isValid and mapPath.isPathMirrorObject();
			
		}else
		{
			isValid = isValid and actIE.isGroupInstance();		
		}		
	}
	
	if(not isValid)
		ergMap.put("ErrorMsg", "Invalid child object detected");
	
	return isValid;
}


operation InternalLink bothPortsHaveCategories() : Boolean
{
	var retMap : Map;
	var categoryA : Attribute = null;
	var categoryB : Attribute = null;
	
	
	self.getPortCategories(retMap);
	
	categoryA = retMap.get("CategoryA");
	categoryB = retMap.get("CategoryB");
		
	return categoryA != null and categoryB != null;
}


operation InternalLink bothPortsHaveSameCategories() : String 
{
	var retMap : Map;
	var categoryA : Attribute = null;
	var categoryB : Attribute = null;
	var errTxt : String;
	
	
	self.getPortCategories(retMap);
	
	categoryA = retMap.get("CategoryA");
	categoryB = retMap.get("CategoryB");	
	
	if(categoryA.value != categoryB.value)
		errTxt = "Category " + categoryA.value + " doesn't match " + categoryB.value;
	
	
	return errTxt;
}


operation InterfaceClass isDerivedFromOrder() : Boolean
{
	var ergMap : Map;
	
	// Anforderung: Abgeleitet von Order, aber keine Basisklasse!
	if(self.isClassChildOf(ergMap, "Order", "InterfaceClass") == true and not isBaseClassDerivedFrom("AutomationMLBaseInterfaces", self.name))
		return true;
	else
		return false;
}

operation ExternalInterface isDerivedFromPortConnector() : Boolean
{
	var ergMap : Map;
	
	// Anforderung: Abgeleitet von Order, aber keine Basisklasse!
	if(self.isClassChildOf(ergMap, "PortConnector", "InterfaceClass") == true and not isBaseClassDerivedFrom("AutomationMLBaseInterfaces", self.name))
	{
		return true;
	}
	else
	{
		return false;
	}
}


operation InterfaceClass isDerivedFromExternalDataConnector() : Boolean
{
	var ergMap : Map;
	
	// Anforderung: Abgeleitet von ExternalDataConnector, aber keine Basisklasse!
	if(self.isClassChildOf(ergMap, "ExternalDataConnector", "InterfaceClass") == true and not isBaseClassDerivedFrom("AutomationMLBaseInterfaces_Extended", self.name))
		return true;
	else
		return false;
}

operation SystemUnitClass hasRoleRequirementFacet() : Boolean
{
	var ergMap : Map;
	var ergMap1 : Map;
	var mapPath : Map;	
	var searchParent : Boolean = false;
	var mirrorSearch : Boolean = false;
	var ret : Boolean;
	var baseRoleClass : RoleClass;
	
	
	//Zunächst den Klassennamen im Rolerequirement parsen
	mapPath = parsePath(self.roleRequirements.refBaseRoleClassPath);
	
	//Dann die konkrete Role-Class ermitteln	
	ret = self.roleRequirements.searchItem(mapPath, searchParent, mirrorSearch, ergMap, "RoleClass");
	baseRoleClass = ergMap.get("obj");
	
	if(baseRoleClass.isClassChildOf(ergMap1, "Facet", "RoleClass") == true ) //and not isBaseClassDerivedFrom("AutomationMLBaseInterfaces_Extended", self.name))
	{		
		return true;
	}
	else
	{	
		return false;
	}
}

operation SystemUnitClass hasRoleRequirementGroup() : Boolean
{
	var ergMap : Map;
	var ergMap1 : Map;
	var mapPath : Map;	
	var searchParent : Boolean = false;
	var mirrorSearch : Boolean = false;
	var ret : Boolean;
	var baseRoleClass : RoleClass;	
	
	//Zunächst den Klassennamen im Rolerequirement parsen
	mapPath = parsePath(self.roleRequirements.refBaseRoleClassPath);
	
	//Dann die konkrete Role-Class ermitteln	
	ret = self.roleRequirements.searchItem(mapPath, searchParent, mirrorSearch, ergMap, "RoleClass");
	baseRoleClass = ergMap.get("obj");
	
	
	if(baseRoleClass.isClassChildOf(ergMap1, "Group", "RoleClass") == true ) //and not isBaseClassDerivedFrom("AutomationMLBaseInterfaces_Extended", self.name))
	{		
		return true;
	}
	else
	{	
		return false;
	}
}






operation InternalElement hasRoleRequirementPort() : Boolean
{
	var ergMap : Map;
	var ergMap1 : Map;
	var mapPath : Map;	
	var searchParent : Boolean = false;
	var mirrorSearch : Boolean = false;
	var ret : Boolean;
	var baseRoleClass : RoleClass;
	
	
	//Zunächst den Klassennamen im Rolerequirement parsen
	mapPath = parsePath(self.roleRequirements.refBaseRoleClassPath);
	
	//Dann die konkrete Role-Class ermitteln	
	ret = self.roleRequirements.searchItem(mapPath, searchParent, mirrorSearch, ergMap, "RoleClass");
	baseRoleClass = ergMap.get("obj");
	
	
	if(baseRoleClass.isClassChildOf(ergMap1, "Port", "RoleClass") == true ) //and not isBaseClassDerivedFrom("AutomationMLBaseInterfaces_Extended", self.name))
	{		
		return true;
	}
	else
	{	
		return false;
	}
}

operation SystemUnitClass hasSupportedRoleClassPort() : Boolean
{
	var ergMap : Map;
	var ergMap1 : Map;
	var mapPath : Map;	
	var searchParent : Boolean = false;
	var mirrorSearch : Boolean = false;
	var ret : Boolean;
	var baseRoleClass : RoleClass;
	
	for (aktSupportedRC in self.supportedRoleClass)
	{	
		//Zunächst den Klassennamen in der SupportedRoleClass parsen
		mapPath = parsePath(aktSupportedRC.refRoleClassPath);
		
		//Dann die konkrete Role-Class ermitteln	
		ret = aktSupportedRC.searchItem(mapPath, searchParent, mirrorSearch, ergMap, "RoleClass");
		baseRoleClass = ergMap.get("obj");	
				
		if(baseRoleClass.isClassChildOf(ergMap1, "Port", "RoleClass") == true ) //and not isBaseClassDerivedFrom("AutomationMLBaseInterfaces_Extended", self.name))
		{
			return true;
		}
	}

	return false;	
}




operation InternalLink getPortCategories(retMap : Map)
{
	var searchParent : Boolean = false;
	var mirrorSearch : Boolean = false;	
	var mapPathSideA : Map;
	var mapPathSideB : Map;	
	var retMapObjA : Map; 
	var retMapObjB : Map;
	var errorMsg : String = "";
	var objA : SystemUnitClass;
	var objB : SystemUnitClass;
	var retMapAttrA : Map; 
	var retMapAttrB : Map;
	var categoryA : Attribute = null;
	var categoryB : Attribute = null;
			
	mapPathSideA = parsePath(self.refPartnerSideA);			
	mapPathSideB = parsePath(self.refPartnerSideB);	
	
	self.searchItem(mapPathSideA, searchParent, mirrorSearch, retMapObjA, "");
	self.searchItem(mapPathSideB, searchParent, mirrorSearch, retMapObjB, "");
	
	objA = retMapObjA.get("obj");
	objB = retMapObjB.get("obj");	
	
	
	if (objA.isPortInstance() and objB.isPortInstance())
	{	
		if(searchAttribute(objA.attribute, "Category", false, retMapAttrA))
			categoryA = retMapAttrA.get("Attr");
			
		if(searchAttribute(objB.attribute, "Category", false, retMapAttrB))
			categoryB = retMapAttrB.get("Attr");
			
		retMap.put("CategoryA", categoryA);
		retMap.put("CategoryB", categoryB);
	}
}



